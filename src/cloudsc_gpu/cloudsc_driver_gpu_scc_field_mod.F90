! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_MOD

  USE PARKIND1, ONLY: JPIM, JPRB
  USE YOMPHYDER, ONLY: STATE_TYPE
  USE YOECLDP, ONLY : NCLV, YRECLDP, TECLDP
  USE CLOUDSC_MPI_MOD, ONLY: NUMPROC, IRANK
  USE TIMER_MOD, ONLY : PERFORMANCE_TIMER, GET_THREAD_NUM

  USE CLOUDSC_GPU_SCC_MOD, ONLY: CLOUDSC_SCC
  USE CLOUDSC_FIELD_STATE_MOD, ONLY: CLOUDSC_FIELD_STATE, COPY_BULK_STORAGE_TO_DEVICE, COPY_BULK_STORAGE_TO_HOST
  USE FIELD_MODULE, ONLY: GET_DEVICE_DATA, ENSURE_HOST, FIELD_3D_VIEW

  IMPLICIT NONE

CONTAINS

  SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD( &
     & NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG, KFLDX, PTSPHY, FIELD_STATE &
     & )
    ! Driver routine that invokes the optimized CLAW-based CLOUDSC GPU kernel

    INTEGER(KIND=JPIM)                                    :: NUMOMP, NPROMA, NLEV, NGPTOT, NGPBLKS, NGPTOTG
    INTEGER(KIND=JPIM)                                    :: KFLDX 
    REAL(KIND=JPRB)                                       :: PTSPHY       ! Physics timestep
    TYPE(CLOUDSC_FIELD_STATE), INTENT(INOUT)              :: FIELD_STATE

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLUDE(:,:,:)    ! Conv. detrained water
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PAPH(:,:,:)     ! Pressure on half levels
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PLSM(:,:)       ! Land fraction (0-1)
    LOGICAL, POINTER, CONTIGUOUS          :: LDCUM(:,:)      ! Convection active
    INTEGER(KIND=JPIM),POINTER, CONTIGUOUS:: KTYPE(:,:)      ! Convection type 0,1,2
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCLV(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PCOVPTOT(:,:,:)    ! Precip fraction
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: PRAINFRAC_TOPRFZ(:,:)

    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PT(:)       ! T at start of callpar
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PQ(:)       ! Q at start of callpar
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PVFA(:)     ! CC from VDF scheme
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PVFL(:)     ! Liq from VDF scheme
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PVFI(:)     ! Ice from VDF scheme
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PDYNA(:)    ! CC from Dynamics
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PDYNL(:)    ! Liq from Dynamics
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PDYNI(:)    ! Liq from Dynamics
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PHRSW(:)    ! Short-wave heating rate
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PHRLW(:)    ! Long-wave heating rate
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PVERVEL(:)  ! Vertical velocity
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PAP(:)      ! Pressure on full levels
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PLU(:)      ! Conv. condensate
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PSNDE(:)    ! Conv. detrained snow
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PMFU(:)     ! Conv. mass flux up
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PMFD(:)     ! Conv. mass flux down
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PA(:)       ! Original Cloud fraction (t)
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PSUPSAT(:)
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PLCRIT_AER(:)
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PICRIT_AER(:)
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PRE_ICE(:)
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PCCN(:)     ! liquid cloud condensation nuclei
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PNICE(:)    ! ice number concentration (cf. CCN)
    ! Flux diagnostics for DDH budget
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQLF(:)    ! Flux of liquid
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQIF(:)    ! Flux of ice
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFCQLNG(:)   ! -ve corr for liq
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFCQNNG(:)   ! -ve corr for ice
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQRF(:)    ! Flux diagnostics
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQSF(:)    !    for DDH, generic
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFCQRNG(:)   ! rain
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFCQSNG(:)   ! snow
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQLTUR(:)  ! liquid flux due to VDF
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFSQITUR(:)  ! ice flux due to VDF
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFPLSL(:)    ! liq+rain sedim flux
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFPLSN(:)    ! ice+snow sedim flux
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFHPSL(:)    ! Enthalpy flux for liq
    TYPE(FIELD_3D_VIEW), ALLOCATABLE  :: PFHPSN(:)    ! ice number concentration (cf. CCN)

    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_LOC_CLD(:,:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_T(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_Q(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_A(:,:,:)
    REAL(KIND=JPRB), POINTER, CONTIGUOUS  :: TEND_TMP_CLD(:,:,:,:)

    INTEGER(KIND=JPIM) :: JKGLO,IBL,ICEND,NBLOCKS
    TYPE(PERFORMANCE_TIMER) :: TIMER
    INTEGER(KIND=JPIM) :: TID ! thread id from 0 .. NUMOMP - 1

    NGPBLKS = (NGPTOT / NPROMA) + MIN(MOD(NGPTOT,NPROMA), 1)
1003 format(5x,'NUMPROC=',i0,', NUMOMP=',i0,', NGPTOTG=',i0,', NPROMA=',i0,', NGPBLKS=',i0)
    if (irank == 0) then
      write(0,1003) NUMPROC,NUMOMP,NGPTOTG,NPROMA,NGPBLKS
    end if

    NBLOCKS = UBOUND(FIELD_STATE%DATA_RDONLY,4)

    ALLOCATE(PFSQLF(NBLOCKS))
    ALLOCATE(PFSQIF(NBLOCKS))
    ALLOCATE(PFCQLNG(NBLOCKS))
    ALLOCATE(PFCQNNG(NBLOCKS))
    ALLOCATE(PFSQRF(NBLOCKS))
    ALLOCATE(PFSQSF(NBLOCKS))
    ALLOCATE(PFCQRNG(NBLOCKS))
    ALLOCATE(PFCQSNG(NBLOCKS))
    ALLOCATE(PFSQLTUR(NBLOCKS))
    ALLOCATE(PFSQITUR(NBLOCKS))
    ALLOCATE(PFPLSL(NBLOCKS))
    ALLOCATE(PFPLSN(NBLOCKS))
    ALLOCATE(PFHPSL(NBLOCKS))
    ALLOCATE(PFHPSN(NBLOCKS))

    ALLOCATE(PT(NBLOCKS))
    ALLOCATE(PQ(NBLOCKS))
    ALLOCATE(PVFA(NBLOCKS))
    ALLOCATE(PVFL(NBLOCKS))
    ALLOCATE(PVFI(NBLOCKS))
    ALLOCATE(PDYNA(NBLOCKS))
    ALLOCATE(PDYNL(NBLOCKS))
    ALLOCATE(PDYNI(NBLOCKS))
    ALLOCATE(PHRSW(NBLOCKS))
    ALLOCATE(PHRLW(NBLOCKS))
    ALLOCATE(PVERVEL(NBLOCKS))
    ALLOCATE(PAP(NBLOCKS))
    ALLOCATE(PLU(NBLOCKS))
    ALLOCATE(PSNDE(NBLOCKS))
    ALLOCATE(PMFU(NBLOCKS))
    ALLOCATE(PMFD(NBLOCKS))
    ALLOCATE(PA(NBLOCKS))
    ALLOCATE(PSUPSAT(NBLOCKS))
    ALLOCATE(PLCRIT_AER(NBLOCKS))
    ALLOCATE(PICRIT_AER(NBLOCKS))
    ALLOCATE(PRE_ICE(NBLOCKS))
    ALLOCATE(PCCN(NBLOCKS))
    ALLOCATE(PNICE(NBLOCKS))

    ! Global timer for the parallel region
    CALL TIMER%START(NUMOMP)

!...Move bulk allocations to device
    CALL COPY_BULK_STORAGE_TO_DEVICE(FIELD_STATE%DATA_RDONLY, FIELD_STATE%DEVDATA_RDONLY)
    CALL COPY_BULK_STORAGE_TO_DEVICE(FIELD_STATE%DATA_RWONLY, FIELD_STATE%DEVDATA_RWONLY)

!...Retrieve device pointers for bulk allocated fields
    CALL FIELD_STATE%F_PT%GET_DEVPTR(PT, FIELD_STATE%DEVDATA_RDONLY, 1)
    CALL FIELD_STATE%F_PQ%GET_DEVPTR(PQ, FIELD_STATE%DEVDATA_RDONLY, 2)
    CALL FIELD_STATE%F_PVFA%GET_DEVPTR(PVFA, FIELD_STATE%DEVDATA_RDONLY, 3)
    CALL FIELD_STATE%F_PVFL%GET_DEVPTR(PVFL, FIELD_STATE%DEVDATA_RDONLY, 4)
    CALL FIELD_STATE%F_PVFI%GET_DEVPTR(PVFI, FIELD_STATE%DEVDATA_RDONLY, 5)
    CALL FIELD_STATE%F_PDYNA%GET_DEVPTR(PDYNA, FIELD_STATE%DEVDATA_RDONLY, 6)
    CALL FIELD_STATE%F_PDYNL%GET_DEVPTR(PDYNL, FIELD_STATE%DEVDATA_RDONLY, 7)
    CALL FIELD_STATE%F_PDYNI%GET_DEVPTR(PDYNI, FIELD_STATE%DEVDATA_RDONLY, 8)
    CALL FIELD_STATE%F_PHRSW%GET_DEVPTR(PHRSW, FIELD_STATE%DEVDATA_RDONLY, 9)
    CALL FIELD_STATE%F_PHRLW%GET_DEVPTR(PHRLW, FIELD_STATE%DEVDATA_RDONLY,10)
    CALL FIELD_STATE%F_PVERVEL%GET_DEVPTR(PVERVEL, FIELD_STATE%DEVDATA_RDONLY,11)
    CALL FIELD_STATE%F_PAP%GET_DEVPTR(PAP, FIELD_STATE%DEVDATA_RDONLY,12)
    CALL FIELD_STATE%F_PLU%GET_DEVPTR(PLU, FIELD_STATE%DEVDATA_RDONLY,13)
    CALL FIELD_STATE%F_PSNDE%GET_DEVPTR(PSNDE, FIELD_STATE%DEVDATA_RDONLY,14)
    CALL FIELD_STATE%F_PMFU%GET_DEVPTR(PMFU, FIELD_STATE%DEVDATA_RDONLY,15)
    CALL FIELD_STATE%F_PMFD%GET_DEVPTR(PMFD, FIELD_STATE%DEVDATA_RDONLY,16)
    CALL FIELD_STATE%F_PA%GET_DEVPTR(PA, FIELD_STATE%DEVDATA_RDONLY,17)
    CALL FIELD_STATE%F_PSUPSAT%GET_DEVPTR(PSUPSAT, FIELD_STATE%DEVDATA_RDONLY,18)
    CALL FIELD_STATE%F_PLCRIT_AER%GET_DEVPTR(PLCRIT_AER, FIELD_STATE%DEVDATA_RDONLY,19)
    CALL FIELD_STATE%F_PICRIT_AER%GET_DEVPTR(PICRIT_AER, FIELD_STATE%DEVDATA_RDONLY,20)
    CALL FIELD_STATE%F_PRE_ICE%GET_DEVPTR(PRE_ICE, FIELD_STATE%DEVDATA_RDONLY,21)
    CALL FIELD_STATE%F_PCCN%GET_DEVPTR(PCCN, FIELD_STATE%DEVDATA_RDONLY,22)
    CALL FIELD_STATE%F_PNICE%GET_DEVPTR(PNICE, FIELD_STATE%DEVDATA_RDONLY,23)

    CALL FIELD_STATE%F_PFSQLF%GET_DEVPTR(PFSQLF, FIELD_STATE%DEVDATA_RWONLY, 1)
    CALL FIELD_STATE%F_PFSQIF%GET_DEVPTR(PFSQIF, FIELD_STATE%DEVDATA_RWONLY, 2)
    CALL FIELD_STATE%F_PFCQLNG%GET_DEVPTR(PFCQLNG, FIELD_STATE%DEVDATA_RWONLY, 3)
    CALL FIELD_STATE%F_PFCQNNG%GET_DEVPTR(PFCQNNG, FIELD_STATE%DEVDATA_RWONLY, 4)
    CALL FIELD_STATE%F_PFSQRF%GET_DEVPTR(PFSQRF, FIELD_STATE%DEVDATA_RWONLY, 5)
    CALL FIELD_STATE%F_PFSQSF%GET_DEVPTR(PFSQSF, FIELD_STATE%DEVDATA_RWONLY, 6)
    CALL FIELD_STATE%F_PFCQRNG%GET_DEVPTR(PFCQRNG, FIELD_STATE%DEVDATA_RWONLY, 7)
    CALL FIELD_STATE%F_PFCQSNG%GET_DEVPTR(PFCQSNG, FIELD_STATE%DEVDATA_RWONLY, 8)
    CALL FIELD_STATE%F_PFSQLTUR%GET_DEVPTR(PFSQLTUR, FIELD_STATE%DEVDATA_RWONLY, 9)
    CALL FIELD_STATE%F_PFSQITUR%GET_DEVPTR(PFSQITUR, FIELD_STATE%DEVDATA_RWONLY,10)
    CALL FIELD_STATE%F_PFPLSL%GET_DEVPTR(PFPLSL, FIELD_STATE%DEVDATA_RWONLY,11)
    CALL FIELD_STATE%F_PFPLSN%GET_DEVPTR(PFPLSN, FIELD_STATE%DEVDATA_RWONLY,12)
    CALL FIELD_STATE%F_PFHPSL%GET_DEVPTR(PFHPSL, FIELD_STATE%DEVDATA_RWONLY,13)
    CALL FIELD_STATE%F_PFHPSN%GET_DEVPTR(PFHPSN, FIELD_STATE%DEVDATA_RWONLY,14)

!...Move individually allocated temporaries to device
    CALL FIELD_STATE%F_PAPH%GET_DEVICE_DATA_RDONLY(PAPH)
    CALL FIELD_STATE%F_PLSM%GET_DEVICE_DATA_RDONLY(PLSM)
    CALL FIELD_STATE%F_LDCUM%GET_DEVICE_DATA_RDONLY(LDCUM)
    CALL FIELD_STATE%F_KTYPE%GET_DEVICE_DATA_RDONLY(KTYPE)
    CALL FIELD_STATE%F_PCLV%GET_DEVICE_DATA_RDONLY(PCLV)
    CALL FIELD_STATE%F_PLUDE%GET_DEVICE_DATA_RDWR(PLUDE)
    CALL FIELD_STATE%F_PCOVPTOT%GET_DEVICE_DATA_RDWR(PCOVPTOT)
    CALL FIELD_STATE%F_PRAINFRAC_TOPRFZ%GET_DEVICE_DATA_RDWR(PRAINFRAC_TOPRFZ)

    CALL FIELD_STATE%TENDENCY_TMP%F_T%GET_DEVICE_DATA_RDONLY(TEND_TMP_T)
    CALL FIELD_STATE%TENDENCY_TMP%F_Q%GET_DEVICE_DATA_RDONLY(TEND_TMP_Q)
    CALL FIELD_STATE%TENDENCY_TMP%F_A%GET_DEVICE_DATA_RDONLY(TEND_TMP_A)
    CALL FIELD_STATE%TENDENCY_TMP%F_CLD%GET_DEVICE_DATA_RDONLY(TEND_TMP_CLD)

    CALL FIELD_STATE%TENDENCY_LOC%F_T%GET_DEVICE_DATA_RDWR(TEND_LOC_T)
    CALL FIELD_STATE%TENDENCY_LOC%F_Q%GET_DEVICE_DATA_RDWR(TEND_LOC_Q)
    CALL FIELD_STATE%TENDENCY_LOC%F_A%GET_DEVICE_DATA_RDWR(TEND_LOC_A)
    CALL FIELD_STATE%TENDENCY_LOC%F_CLD%GET_DEVICE_DATA_RDWR(TEND_LOC_CLD)

!$acc data copyin(yrecldp)

    ! Local timer for each thread
    TID = GET_THREAD_NUM()
    CALL TIMER%THREAD_START(TID)

!$acc parallel loop gang vector_length(NPROMA)
    DO JKGLO=1,NGPTOT,NPROMA
       IBL=(JKGLO-1)/NPROMA+1
       ICEND=MIN(NPROMA,NGPTOT-JKGLO+1)

       CALL CLOUDSC_SCC &
        & (1, ICEND, NPROMA, NLEV, PTSPHY,&
        & PT(IBL)%P, PQ(IBL)%P, &
        & TEND_TMP_T(:,:,IBL), TEND_TMP_Q(:,:,IBL), TEND_TMP_A(:,:,IBL), TEND_TMP_CLD(:,:,:,IBL), &
        & TEND_LOC_T(:,:,IBL), TEND_LOC_Q(:,:,IBL), TEND_LOC_A(:,:,IBL), TEND_LOC_CLD(:,:,:,IBL), &
        & PVFA(IBL)%P, PVFL(IBL)%P, PVFI(IBL)%P, PDYNA(IBL)%P, PDYNL(IBL)%P, PDYNI(IBL)%P, &
        & PHRSW(IBL)%P,    PHRLW(IBL)%P,&
        & PVERVEL(IBL)%P,  PAP(IBL)%P,      PAPH(:,:,IBL),&
        & PLSM(:,IBL),       LDCUM(:,IBL),      KTYPE(:,IBL), &
        & PLU(IBL)%P,      PLUDE(:,:,IBL),    PSNDE(IBL)%P,    PMFU(IBL)%P,     PMFD(IBL)%P,&
        !---prognostic fields
        & PA(IBL)%P,       PCLV(:,:,:,IBL),   PSUPSAT(IBL)%P,&
        !-- arrays for aerosol-cloud interactions
        & PLCRIT_AER(IBL)%P,PICRIT_AER(IBL)%P,&
        & PRE_ICE(IBL)%P,&
        & PCCN(IBL)%P,     PNICE(IBL)%P,&
        !---diagnostic output
        & PCOVPTOT(:,:,IBL), PRAINFRAC_TOPRFZ(:,IBL),&
        !---resulting fluxes
        & PFSQLF(IBL)%P,   PFSQIF(IBL)%P,  PFCQNNG(IBL)%P,  PFCQLNG(IBL)%P,&
        & PFSQRF(IBL)%P,   PFSQSF(IBL)%P,  PFCQRNG(IBL)%P,  PFCQSNG(IBL)%P,&
        & PFSQLTUR(IBL)%P, PFSQITUR(IBL)%P, &
        & PFPLSL(IBL)%P,   PFPLSN(IBL)%P,  PFHPSL(IBL)%P, PFHPSN(IBL)%P,&
        & YRECLDP=YRECLDP)

    ENDDO
!$acc end parallel loop
!$acc end data
    
    CALL TIMER%THREAD_END(TID)

!...Move individually allocated temporaries back to host
    CALL FIELD_STATE%F_PLUDE%ENSURE_HOST()
    CALL FIELD_STATE%F_PCOVPTOT%ENSURE_HOST()
    CALL FIELD_STATE%F_PRAINFRAC_TOPRFZ%ENSURE_HOST()
    CALL FIELD_STATE%TENDENCY_LOC%F_T%ENSURE_HOST()
    CALL FIELD_STATE%TENDENCY_LOC%F_Q%ENSURE_HOST()
    CALL FIELD_STATE%TENDENCY_LOC%F_A%ENSURE_HOST()
    CALL FIELD_STATE%TENDENCY_LOC%F_CLD%ENSURE_HOST()

!...Move bulk allocations back to host
    CALL COPY_BULK_STORAGE_TO_HOST(FIELD_STATE%DATA_RWONLY, FIELD_STATE%DEVDATA_RWONLY)

    CALL TIMER%END()

    DEALLOCATE(PFSQLF)
    DEALLOCATE(PFSQIF)
    DEALLOCATE(PFCQLNG)
    DEALLOCATE(PFCQNNG)
    DEALLOCATE(PFSQRF)
    DEALLOCATE(PFSQSF)
    DEALLOCATE(PFCQRNG)
    DEALLOCATE(PFCQSNG)
    DEALLOCATE(PFSQLTUR)
    DEALLOCATE(PFSQITUR)
    DEALLOCATE(PFPLSL)
    DEALLOCATE(PFPLSN)
    DEALLOCATE(PFHPSL)
    DEALLOCATE(PFHPSN)

    DEALLOCATE(PT)
    DEALLOCATE(PQ)
    DEALLOCATE(PVFA)
    DEALLOCATE(PVFL)
    DEALLOCATE(PVFI)
    DEALLOCATE(PDYNA)
    DEALLOCATE(PDYNL)
    DEALLOCATE(PDYNI)
    DEALLOCATE(PHRSW)
    DEALLOCATE(PHRLW)
    DEALLOCATE(PVERVEL)
    DEALLOCATE(PAP)
    DEALLOCATE(PLU)
    DEALLOCATE(PSNDE)
    DEALLOCATE(PMFU)
    DEALLOCATE(PMFD)
    DEALLOCATE(PA)
    DEALLOCATE(PSUPSAT)
    DEALLOCATE(PLCRIT_AER)
    DEALLOCATE(PICRIT_AER)
    DEALLOCATE(PRE_ICE)
    DEALLOCATE(PCCN)
    DEALLOCATE(PNICE)

    ! On GPUs, adding block-level column totals is cumbersome and
    ! error prone, and of little value due to the large number of
    ! processing "thread teams". Instead we register the total here.
    CALL TIMER%THREAD_LOG(TID=TID, IGPC=NGPTOT)

    CALL TIMER%PRINT_PERFORMANCE(NPROMA, NGPBLKS, NGPTOT)

  END SUBROUTINE CLOUDSC_DRIVER_GPU_SCC_FIELD

END MODULE CLOUDSC_DRIVER_GPU_SCC_FIELD_MOD
